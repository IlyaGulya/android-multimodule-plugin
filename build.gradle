plugins {
    id 'org.jetbrains.intellij' version '0.4.18'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group 'ru.hh.android.plugins'
version '4.0'

repositories {
    mavenCentral()
    maven {
        url "https://packages.atlassian.com/maven/repository/public"
    }
}

apply from: 'product_matrix.gradle'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "commons-io:commons-io:2.4"
    implementation "com.vladsch.flexmark:flexmark-all:0.50.42"

    implementation("com.atlassian.jira:jira-rest-java-client-core:4.0.0") {
        exclude group: 'org.slf4j'
        dependencies {
            implementation "com.atlassian.fugue:fugue:2.6.1"
        }
    }
}

// region Plugin compatibility setup
def currentVersion = productVersions['local_path']

intellij.type = 'IC'
if (currentVersion.isLocal) {
    intellij.localPath = currentVersion.ideVersion
} else {
    intellij.version = currentVersion.ideVersion
}
intellij.plugins = currentVersion.plugins
// endregion

runIde {
    // Absolute path to installed target v3.6.3 Android Studio to use as IDE Development Instance
    // The "Contents" directory is macOS specific.
    ideDirectory '/Applications/Android Studio.app/Contents'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}